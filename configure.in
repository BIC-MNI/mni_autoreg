dnl Process this file with autoconf to produce a configure script

AC_INIT(minctracc/Include/minctracc.h)
AM_INIT_AUTOMAKE(mni_autoreg, 0.98h)
AM_CONFIG_HEADER(config.h)

# If prefix is set, use it to set CPPFLAGS and LDFLAGS.
# This helps when probing for libraries, later on, in the common case
# that the 3rd party libraries are also installed in the place where
# this package is going.
#
if test "$prefix" != "NONE"; then
    if test "$CPPFLAGS" = "" -a -d "${prefix}/include"; then
        CPPFLAGS="-I${prefix}/include"
	# volume_io is weird; we'll hack it for now
        test -d "${prefix}/include/volume_io" && \
		CPPFLAGS="$CPPFLAGS -I${prefix}/include/volume_io"
    fi
    if test "$LDFLAGS" = "" -a -d "${prefix}/lib"; then
        LDFLAGS="-L${prefix}/lib"
    fi
fi

#------------------------------------------------------------------------
# Define symbols specifying the package version number,
# compilation user, host, date, and time.
#------------------------------------------------------------------------

# The terminology here is a little icky: what most people think of as
# "host" (ie. the current machine name) is called "machine", because
# "host" is coopted by autoconf's AC_CANONICAL_HOST macro.  It's 
# used to identify the system, e.g. "mips-sgi-irix5.3" -- which *I*
# think should be called "system", so that's what I call it in
# version.h.  Still, kind of ugly.

AC_CANONICAL_HOST
year=`date | awk '{print $NF}'`        # ugh -- SunOS "date" has no %Y
date=`date +%m-%d`

AC_DEFINE_UNQUOTED(MNI_AUTOREG_COMPILE_TIME,"`date +%T`",[Configure Time])
AC_DEFINE_UNQUOTED(MNI_AUTOREG_COMPILE_DATE,"$year-$date",[Configure Date])
AC_DEFINE_UNQUOTED(MNI_AUTOREG_COMPILE_USER,"`logname`",[Configure User])
AC_DEFINE_UNQUOTED(MNI_AUTOREG_COMPILE_HOST,"`hostname`",[Configure Host])
AC_DEFINE_UNQUOTED(MNI_AUTOREG_COMPILE_SYSTEM,"$host",[Configure System])



#------------------------------------------------------------------------
# Checks for compiler toolset.
#------------------------------------------------------------------------
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_MAKE_SET
AC_PROG_RANLIB

#------------------------------------------------------------------------
# Look for perl (and make sure it's the right version)
#------------------------------------------------------------------------

if test -z "$PERL"; then
  AC_PATH_PROGS(PERL, [perl5 perl], no)
fi
AC_SUBST(PERL)
if test "$PERL" = no; then
  AC_MSG_WARN(The mritotal script will not run since Perl 5 is not found. 
   You must install Perl 5 before installing MNI AutoReg.)
  PERL=
else
  AC_MSG_CHECKING(Perl version)

  # The perl command will exit with status zero if
  #  (i) $PERL is found, and executable, and
  # (ii) the perl version is NOT < 5.002
  #
  if $PERL -e 'exit ($] < 5.002)'; then
    AC_MSG_RESULT(5.002 or later--good)
  else
    AC_MSG_RESULT(unacceptable)
    AC_MSG_WARN(Perl 5.002 (or greater) is required--you will not be able to run mritotal)
    PERL=
  fi
fi

AM_CONDITIONAL(PERLGOOD, test x$PERL != x)


# -----------------------------------------------------------------
# Check the OS -- if IRIX (SGI), we can use cc and get an ANSI 
# compiler.  Otherwise, the user has to know what they're doing
# in order to override gcc.
# -----------------------------------------------------------------

dnl SMR: removed this, since released automake does not
dnl handle dependency tracking with IRIX cc.
#case "$host_os" in
#  irix*) if test -z "$CC"; then CC=cc; fi ;;
#esac


# -----------------------------------------------------------------
# Look for the C compiler (default gcc, except on SGI's), and
# make sure it supports prototypes, void, enum, and signed char
# properly (can't trust __STDC__).
# -----------------------------------------------------------------

cc_flagged=`echo "$CC $CFLAGS $CPPFLAGS" |sed 's/ +/ /;s/ *$//'`
AC_MSG_CHECKING(that \"$cc_flagged\" has enough ANSI in it)
AC_CACHE_VAL(autoreg_cv_cc_ansi, [
AC_TRY_COMPILE([
typedef enum { RED, GREEN, BLUE, YELLOW, BLACK } colour;
], [
signed char signed_char_name;

void *foo (colour C);
colour a = GREEN;
 
foo (a);
], autoreg_cv_cc_ansi=yes, autoreg_cv_cc_ansi=no)])
CC_ANSI=$autoreg_cv_cc_ansi
AC_MSG_RESULT($CC_ANSI)
 
if test $CC_ANSI = no; then
  AC_MSG_CHECKING(for alternative C compiler)
  AC_CHECK_PROGS(ALTCC, acc gcc, none)
  if test $ALTCC = none; then
    AC_MSG_ERROR(An ANSI-compliant C compiler is required to build the 
                 MNI AutoReg package)
  else
    CC=$ALTCC
  fi
fi

#------------------------------------------------------------------------
# Check that we have some required header files (and macros in them)
#------------------------------------------------------------------------
AC_HEADER_STDC
AC_CHECK_HEADERS(float.h limits.h malloc.h)

# We need to know extreme values for various data types
# On POSIX systems, these are defined in <float.h> (floats, doubles,
# and long doubles) and in <limits.h> for integer types.
#
# Some older systems might have similar macros defined in
# <values.h>, so we check for that, too.
#
if test $ac_cv_header_limits_h = no \
     -o $ac_cv_header_float_h = no; then
	AC_CHECK_HEADER(values.h, , 
			AC_MSG_ERROR(either limits.h or values.h is required.))
fi


#------------------------------------------------------------------------
# Checks for typedefs, structures, and compiler characteristics.
#------------------------------------------------------------------------
AC_C_INLINE
AC_C_CONST
AC_TYPE_SIZE_T

#------------------------------------------------------------------------
# Checks for libraries.
#------------------------------------------------------------------------
AC_CHECK_LIB(c, xdr_long,:,AC_CHECK_LIB(sun, xdr_long))
AC_CHECK_LIB(c_s, main)
AC_CHECK_LIB(ucb, random)

smr_CHECK_LIB(netcdf,,
[NetCDF library], ncopen, netcdf.h)
if test "$netcdf_LIBS" = ""; then
    AC_MSG_ERROR([Required library NetCDF not found!])
fi

smr_CHECK_LIB(minc,,
[MNI medical image library], miicv_create, minc.h, 
$netcdf_LIBS, $netcdf_CFLAGS)
if test "$minc_LIBS" = ""; then
    AC_MSG_ERROR([Required library minc not found!])
fi

smr_CHECK_LIB(volumeio,volume_io,
[MNI Volume IO library], input_volume, volume_io.h,
[$netcdf_LIBS $minc_LIBS -lm],[$netcdf_CFLAGS $minc_CFLAGS])
if test "$volumeio_LIBS" = ""; then
    AC_MSG_ERROR([Required library volume_io not found!])
fi

INCLUDEDIRS="$volumeio_CFLAGS $minc_CFLAGS $netcdf_CFLAGS"
LIBDIRS="$volumeio_LIBS $minc_LIBS $netcdf_LIBS"

AC_SUBST(INCLUDEDIRS)
AC_SUBST(LIBDIRS)

#------------------------------------------------------------------------
# Check some oddities of the volume IO library.
#------------------------------------------------------------------------

save_CPPFLAGS="$CPPFLAGS"
save_LIBS="$LIBS"
CPPFLAGS="$CPPFLAGS $INCLUDEDIRS"
LIBS="$LIBS $LIBDIRS -lvolume_io -lminc -lnetcdf -lm"

# Test if volume data structure has array.data member (CURRNENT), 
# or just data member (OLD).
AC_MSG_CHECKING(how to access raw array of a volume)
autoreg_test="volume->array.data"
AC_TRY_COMPILE( [#include <volume_io.h>], 
		[Volume v; v->array.data = NULL;],
		AC_DEFINE(VOLIO_HAVE_VOLUME_ARRAY_DATA, 1, [Volume data stored in array.data]),
		autoreg_test=fail )

if test $autoreg_test = "fail"; then
    autoreg_test="volume->data"
    AC_TRY_COMPILE( [#include <volume_io.h>], 
		    [Volume v; v->data = NULL;],
		    AC_DEFINE(VOLIO_HAVE_VOLUME_DATA, 1, [Volume data stored in data]),
		    autoreg_test=fail )
fi

AC_MSG_RESULT($autoreg_test)
if test $autoreg_test = "fail"; then
    AC_MSG_ERROR(Cannot determine how to access raw data in a volume.)
fi


# Test if format_time is called as
#   string = format_time( format, time )   CURRENT
# or
#   format_time( string, format, time )    OLD
#
autoreg_test="2"
AC_MSG_CHECKING(number of arguments of format_time)
AC_TRY_COMPILE( [#include <volume_io.h>], 
		[STRING s = format_time( "%g %s", 0 );],
		AC_DEFINE( VOLIO_HAVE_2ARG_FORMAT_TIME, 1, [format_time takes 2 arguments] ),
		autoreg_test=fail )

if test $autoreg_test = "fail"; then
    autoreg_test="3"
    AC_TRY_COMPILE( [#include <volume_io.h>], 
		    [STRING s; format_time( s, "%g %s", 0 );],
		    AC_DEFINE( VOLIO_HAVE_3ARG_FORMAT_TIME, 1, [format_time takes 3 arguments] ),
		    autoreg_test=fail )
fi

AC_MSG_RESULT($autoreg_test)
if test $autoreg_test = "fail"; then
    AC_MSG_ERROR(Cannot determine arguments to format_time.)
fi

# Test if delete_dimension_names takes two arguments (CURRENT),
# or one (OLD).
#
autoreg_test="2"
AC_MSG_CHECKING(number of arguments of delete_dimension_names)
AC_TRY_COMPILE( [#include <volume_io.h>], 
		[Volume v; char** names; delete_dimension_names(v,names);],
		AC_DEFINE( VOLIO_HAVE_2ARG_DELETE_DIMENSION_NAMES, 1, [function takes 2 arguments] ),
		autoreg_test=fail )

if test $autoreg_test = "fail"; then
    autoreg_test="1"
    AC_TRY_COMPILE( [#include <volume_io.h>], 
		    [Volume v; char** names; delete_dimension_names(v,names);],
		    AC_DEFINE( VOLIO_HAVE_1ARG_DELETE_DIMENSION_NAMES, 1, [function takes 1 argument] ),
		    autoreg_test=fail )
fi

AC_MSG_RESULT($autoreg_test)
if test $autoreg_test = "fail"; then
    AC_MSG_ERROR(Cannot determine arguments to delete_dimension_names.)
fi

dnl This bit of magic was borrowed from XEmacs via FVWM.
dnl If the program compiles and runs, we run again with a dummy argument
dnl that outputs the RCSID revision number.
dnl Note that this check is only informational.  The source code only
dnl depends on the more specific tests, above.
AC_MSG_CHECKING(revision of volume_io.h)
AC_TRY_RUN([
    #include <string.h>
    #include <volume_io.h>
    int main(int c, char **v) {
	char* s = strstr(volume_io_rcsid,"volume_io.h,v ");
	char* t;
	if ( s == NULL ) return 1;
	s += 14;
	t = strchr(s,' ');
	if ( t == NULL ) return 2;
	*t = 0;
	if ( c > 1 ) printf("%s\n", s);
	return 0;
    }],
    [vio_ver=`./conftest dummy_arg`],
    [AC_MSG_RESULT(unknown)
     AC_MSG_ERROR([Cannot determine version.  See config.log for details.])])

CPPFLAGS="$save_CPPFLAGS"
LIBS="$save_LIBS"

if test x$vio_ver = x; then
    AC_MSG_RESULT(unknown)
    AC_MSG_WARN([you seem to have an ancient version of Volume_io; if you have problems compiling, please upgrade])
else
    AC_MSG_RESULT($vio_ver)
    case $vio_ver in
    	1.[[1-5]]) 
	    AC_MSG_WARN([you have an old version of Volume_io; if you have problems compiling, please upgrade])
	    ;;
    	1.6) 
	    AC_MSG_RESULT([you have the version distributed with MINC 0.3 -- no problem!]) 
	    ;;
    	*)  AC_MSG_RESULT([you have an up-to-date version -- excellent!])
	    ;;
    esac
fi


#------------------------------------------------------------------------
# Checks for library functions.
#------------------------------------------------------------------------
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(strtod strtol)


AC_OUTPUT(Makefile
Proglib/Makefile
make_phantom/Makefile
mincbbox/Makefile
mincblur/Makefile
minctracc/Makefile
minctracc/Files/Makefile
minctracc/Main/Makefile
minctracc/Numerical/Makefile
minctracc/Optimize/Makefile
minctracc/Volume/Makefile
minctracc/Extra_progs/Makefile
perl/Makefile
Testing/Makefile
)
